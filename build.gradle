plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
}

dependencies {
    implementation 'junit:junit:4.12'
    implementation 'io.cucumber:cucumber-java:6.8.1'
    implementation 'io.cucumber:cucumber-java8:6.8.1'
}

repositories {
    mavenCentral()
}

// Cannot directly access the implementation configuration, so extend it for use in creating jar and running cucumber
configurations {
    cucumberRuntime {
        extendsFrom implementation
    }
}

// Fat jar, with all cucumber implementation dependencies added
shadowJar {
    manifest {
        attributes "Main-Class": "com.foo.RunCukes"
    }
    archiveFileName = 'run-cukes.jar'
    // Required to merge all the backend services: io.cucumber.core.backend.BackendProviderService
    mergeServiceFiles()
}

// Task to run cucumber, implementation configuration plus main sources are added to class path
task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "com.foo.RunCukes"
            classpath = configurations.cucumberRuntime + sourceSets.main.output
        }
    }
}